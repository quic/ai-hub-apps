plugins {
    id 'com.android.application' version '8.13.0'
}

android {
    ndkVersion '27.3.13750724'
    compileSdk 34

    defaultConfig {
        applicationId "com.quicinc.imageclassification"
        minSdk 31
        targetSdk 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue( "string", "tfLiteModelAsset", "classifier.tflite")
        resValue( "string", "tfLiteLabelsAsset", "labels.txt")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    preBuild.doFirst {
        if (!file("./src/main/assets/classifier.tflite").exists()) {
            throw new RuntimeException("classifier.tflite is missing. Please refer to the app README for instructions on how to compile and place the model(s).")
        }

        for (int i = 1; i <= 3; ++i) {
            String filename = "./src/main/assets/images/Sample${i}.png"
            File fp = file(filename)
            // If the image is too small, it is likely an ascii file with LFS information
            if (!fp.exists() || fp.length() < 1000) {
                throw new RuntimeException("Project assets are missing. Please make sure the git repository is cloned with LFS (https://git-lfs.com/) enabled.")
            }
        }
    }

    namespace 'com.quicinc.imageclassification'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'org.tensorflow:tensorflow-lite:2.17.0'
    api 'org.tensorflow:tensorflow-lite-support:0.5.0'
    implementation "org.tensorflow:tensorflow-lite-gpu:2.17.0"
    implementation "org.tensorflow:tensorflow-lite-gpu-api:2.17.0"
    implementation 'org.tensorflow:tensorflow-lite-gpu-delegate-plugin:0.4.4'
    implementation "com.qualcomm.qti:qnn-runtime:2.40.0"
    implementation "com.qualcomm.qti:qnn-litert-delegate:2.40.0"
}

// Include utilities needed by the AI Hub Apps test suite.
if (file('../common.gradle').exists()) {
    apply from: '../common.gradle'
}
